plugins {
  id 'java-library'
  id 'eclipse'
  id 'idea'
  id 'maven-publish'
  id 'net.neoforged.gradle.userdev' version '7.0.165'
  id "me.shedaniel.unified-publishing" version "0.1.13"
  id "io.freefair.lombok" version "8.6"
}

tasks.named('wrapper', Wrapper).configure {
  distributionType = Wrapper.DistributionType.BIN
}

tasks.build {
  finalizedBy(tasks.publishToMavenLocal)
}

base {
  archivesName = archives_base_name
  version = "${minecraft_version}-${mod_version}"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)
java.withSourcesJar()

File transformer = rootProject.file("src/main/resources/META-INF/accesstransformer.cfg")

if (transformer.exists()) {
  minecraft.accessTransformers.file transformer
}

runs {
  configureEach {
    systemProperty 'forge.logging.markers', 'REGISTRIES'
    systemProperty 'forge.logging.console.level', 'debug'

    modSource project.sourceSets.main
  }

  client {
    systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
  }

  server {
    systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    argument '--nogui'
  }

  gameTestServer {
    systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
  }

  data {
    arguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
  }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
  runtimeClasspath.extendsFrom localRuntime
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
  maven {
    name = "Jared's maven"
    url = "https://maven.blamejared.com/"
    content {
      includeGroup "mezz.jei"
//      includeGroup "com.hollingsworth.ars_nouveau"
//      includeGroup "vazkii.patchouli"
    }
  }
  maven {
    name = "Lat's maven"
    url = "https://maven.saps.dev/releases"
    content {
      includeGroup "dev.latvian.mods"
      includeGroup "dev.latvian.apps"
    }
  }
  maven {
    url = "https://maven.architectury.dev"
    content {
      includeGroup "dev.architectury"
    }
  }
  maven {
    name = "Mod maven"
    url = "https://modmaven.dev/"
    content {
      includeGroup "mcjty.theoneprobe"
      includeGroup "mekanism"
      includeGroup "appeng"
    }
  }
  maven {
    url "https://www.cursemaven.com"
    content {
      includeGroup "curse.maven"
    }
  }
  maven {
    name = "OctoStudios"
    url = uri("https://maven.octo-studios.com/releases")
  }
  //Needed for KubeJS
  maven {
    url 'https://jitpack.io'
    content {
      includeGroup "com.github.rtyley"
    }
  }
  maven {
    name = "TerraformersMC"
    url = "https://maven.terraformersmc.com/"
    content {
      includeGroup "dev.emi"
    }
  }
}

dependencies {
  // lombok
  compileOnly "org.projectlombok:lombok:${project.lombok_version}"
  annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
  implementation "net.neoforged:neoforge:${neo_version}"
  implementation "es.degrassi:experiencelib:${minecraft_version}-${experiencelib_version}"

  // ProbeJS
  localRuntime("curse.maven:probejs-585406:5820894")

  // JEI
  compileOnly("mezz.jei:jei-${minecraft_version}-neoforge-api:${jei_version}")
  compileOnly localRuntime("mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}")

  // EMI
  compileOnly("dev.emi:emi-neoforge:${emi_version}+${minecraft_version}:api")
  compileOnly runtimeOnly("dev.emi:emi-neoforge:${emi_version}+${minecraft_version}")

  // KubeJS
  compileOnly localRuntime("dev.latvian.mods:kubejs-neoforge:${kubejs_version}")

  // Jade
  compileOnly localRuntime("curse.maven:jade-324717:${jade_version}")

  // The one probe
  localRuntime compileOnly("mcjty.theoneprobe:theoneprobe:${top_version}") {transitive = false}

  // Mekanism
  localRuntime("mekanism:Mekanism:${mekanism_version}")

  // Addons for testing
  implementation("appeng:appliedenergistics2:${ae2_version}")

  // ExperienceFluids
  localRuntime("curse.maven:mob-grinding-utils-254241:${project.MGU}")
  localRuntime("curse.maven:titanium-287342:${titanium}")
  localRuntime("curse.maven:industrial-foregoing-266515:${project.IF}")
}

tasks.withType(ProcessResources).configureEach {
  var replaceProperties = [
      minecraft_version      : minecraft_version,
      minecraft_version_range: minecraft_version_range,
      neo_version            : neo_version,
      neo_version_range      : neo_version_range,
      loader_version_range   : loader_version_range,
      mod_id                 : mod_id,
      mod_name               : mod_name,
      mod_license            : mod_license,
      mod_version            : mod_version,
      mod_authors            : mod_authors,
      mod_description        : mod_description,
      mod_url                : mod_url,
      ae2_version            : ae2_version,
      experiencelib_version  : experiencelib_version,
      icon                   : icon,
      pack_format_number     : pack_format_number
  ]
  inputs.properties replaceProperties

  filesMatching(['META-INF/neoforge.mods.toml']) {
    expand replaceProperties
  }
}

// Example configuration to allow publishing using the maven-publish plugin
publishing {
  publications {
    register('mavenJava', MavenPublication) {
      groupId = maven_group
      artifactId = archivesBaseName
      version = project.version
      from components.java

      pom {
        name = mod_name
        description = mod_description
        packaging = 'jar'
        scm {
          url = mod_github
        }
        issueManagement {
          system = 'github'
          url = mod_issues
        }
        licenses {
          license {
            name = mod_license
            distribution = 'repo'
          }
        }
        developers {
          developer {
            id = mod_authors
            name = mod_authors
            roles = ['developper']
          }
        }
      }
    }
  }
  repositories {
    maven {
      name = 'maven'
      url = 'D:\\local_maven'
    }
  }
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
  module {
    downloadSources = true
    downloadJavadoc = true
  }
}

unifiedPublishing {
  project {
    displayName = "[NeoForge]AppliedExperienced-${minecraft_version}-${mod_version}"
    releaseType = "release"
    if (rootProject.file("CHANGELOG.md").exists()) {
      changelog = rootProject.file("CHANGELOG.md").text
    }
    gameVersions = ["${minecraft_version}", "1.21"]
    gameLoaders = ["neoforge"]
    mainPublication jar

    var CURSE_API_KEY = System.getenv("CURSEFORGE")
    if (CURSE_API_KEY != null) {
      curseforge {
        token = CURSE_API_KEY
        id = "1157608"
        gameVersions.addAll "Java 21"
        relations {
          depends "applied-energistics-2"
          depends "experiencelib"
        }
      }
    }

    var MODRINTH_API_KEY = System.getenv("MODRINTH")
    if (MODRINTH_API_KEY != null) {
      modrinth {
        token = MODRINTH_API_KEY
        id = "yKwUnZCV"
        relations {
          depends "ae2"
          depends "experiencelib"
        }
      }
    }
  }
}
